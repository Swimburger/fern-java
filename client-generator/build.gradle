plugins {
    id 'java-library-distribution'
    id "com.palantir.docker" version "0.33.0"
    id "org.unbroken-dome.test-sets" version "4.0.0"
    id 'java'
}

dependencies {
    implementation project(':generator-utils')
    implementation project(':runtime-utils:jersey-utils')

    implementation 'ch.qos.logback:logback-classic'
    implementation 'com.squareup.okhttp3:okhttp'

    compileOnly 'org.immutables:value::annotations'
    annotationProcessor 'org.immutables:value'

    testImplementation project(':testing-utils')
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'io.github.origin-energy:java-snapshot-testing-junit5'
}


distributions {
    main {
        distributionBaseName = project.name
    }
}

docker {
    name "java-client:${project.version}"
    tags 'latest', "${project.version}"
    files tasks.distTar.outputs, './init.sh', './gradle-cache-project/build.gradle'
    buildArgs([BUILD_VERSION: "${project.version}"])
}

task dockerTagVersion {
    dependsOn ":client-generator:dockerTag${project.version}"
}

testSets {
    eteTest
}

eteTest {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task deleteSnapshot(type: Exec) {
    commandLine 'rm', '-rf', 'src/eteTest/java/com/fern/java/client/cli/__snapshots__/ClientGeneratorEteTest.snap'
    commandLine 'rm', '-rf', 'src/eteTest/java/com/fern/java/client/cli/__snapshots__/LocalClientGeneratorEteTest.snap'
}

rootProject.tasks.build.dependsOn eteTest
tasks.eteTest.dependsOn ':client-generator:dockerTagLatest'